# D (Dlang) {#dlang}

Nixpkgs provides multiple D compilers such as `ldc` and `dmd`.
These can be used like any other package during build time.


## `dub` {#dub}

For the `dub` package manager / build system, there are some helpful hooks and utilities for building packages.

* `dubSetupHook`: Hook that populates your $DUB_HOME with the dependencies specified in the attribute `dubDeps`.
        See `importDubLock` below for how create a derivation `dubDeps` can be set to.
* `dubBuildHook`: Hook that runs `dub build` in the buildPhase with flags from `dubBuildFlags` and `dubFlags`.
        You can set `dubBuildType` to specify the build type (`release` by default).
        Note: you still have to add your desired D compiler to the `nativeBuildInputs`.
* `dubCheckHook`: Hook that runs `dub test` in the checkPhase with flags from `dubTestFlags` and `dubFlags`.
        Don't forget to set `doCheck = true`
* `importDubLock`: Function that creates an acceptable package cache for `dub` from the lockfile provided.
        It takes in the `pname` and `version` of the parent package, along with the lockfile itself as the `lock` attribute.
        The lockfile is generated by `dub-to-nix` from the source of the package (see below).
        You can set `lock` to the the lockfile's path, or the parsed lockfile contents itself (e.g. read by `importJSON`)

Note that you typically need to define a custom `installPhase` for installing your binaries into `$out`.

## `dub` lockfiles {#dub-lockfiles}
Nixpkgs has its own lockfile format for `dub` dependencies, because `dub`'s official "lockfile" format (`dub.selections.json`) is not hash-based.

A lockfile can be generated using the `dub-to-nix` helper package.
* Firstly, install `dub-to-nix` into your shell session by running `nix-shell -p dub-to-nix`.
* Then navigate to the root of the source of the program you want to package.
* Finally, run `dub-to-nix`, and it will print the lockfile to stdout. You can pipe stdout into a text file or just copy the output manually into a file.

## `buildDubPackage` {#builddubpackage}

Nixpkgs also provides a build helper called `buildDubPackage` that combines all the hooks and utilities mentioned above.
You can still set the attributes mentioned in the hook descriptions to control your build.

In `buildDubPacakge` `dubDeps` shouldn't be specified directly. Instead, set `dubLock` to the value you'd want to pass to `importDubLock { lock = ...; }`
Also, you can specify a compiler via the `compiler` attribute: it will be added  The D compiler to be used by `dub`.

Here's an example:
```nix
{
  lib,
  buildDubPackage,
  fetchFromGitHub,
  ncurses,
  zlib,
}:

buildDubPackage rec {
  pname = "btdu";
  version = "0.5.1";

  src = fetchFromGitHub {
    owner = "CyberShadow";
    repo = "btdu";
    tag = "v${version}";
    hash = "sha256-3sSZq+5UJH02IO0Y1yL3BLHDb4lk8k6awb5ZysBQciE=";
  };

  dubLock = ./dub-lock.json;

  buildInputs = [
    ncurses
    zlib
  ];

  installPhase = ''
    runHook preInstall
    install -Dm755 btdu -t $out/bin
    runHook postInstall
  '';
}
```

## `buildDubPackage` parameters {#builddubpackage-parameters}

The `buildDubPackage` function takes an attrset of parameters that are passed on to `stdenv.mkDerivation`.

Since `buildDubPackage` uses the hooks mentioned above, you can still set the same attributes to control your build.

The following parameters are specific to `buildDubPackage`:
* `dubLock`: This should be set instead of setting `dubDeps` directly, shorthand for `importDubLock { lock = ...; }`.
* `compiler ? ldc`: The D compiler to be used by `dub`.

Additionally references to the provided `compiler` are stripped from the binaries in $out

